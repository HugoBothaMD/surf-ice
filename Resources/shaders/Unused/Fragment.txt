//pref
Ambient|float|0|0.35|1
Diffuse|float|0|0.9|1
Specular|float|0|0.4|1
DiffuseRough|float|0|1|5
SpecularRough|float|0.001|0.1|1
Sharpness|float|0|0.0|1
Edge|float|0|0|1.0
LightBackfaces|bool|false
Blinn-Phong shading. Adapted from Fluxus Library, Copyright 2007 Dave Griffiths, GPLv2.|note
//vert
#version 330
layout(location = 0) in vec3 Vert;
layout(location = 3) in vec3 Norm;
layout(location = 6) in vec4 Clr;
out vec4 vClr, vP;
uniform mat4 ModelViewProjectionMatrix;
uniform mat4 ModelViewMatrix;
uniform mat3 NormalMatrix;
uniform float Ambient, Diffuse, Specular, SpecularRough, DiffuseRough, Edge, Sharpness;
uniform bool LightBackfaces;

uniform vec3 LightPos = vec3(0.0, 20.0, 30.0); //LR, -DU+, -FN+
vec3 desaturate(vec3 color, float amount) {
    vec3 gray = vec3(dot(vec3(0.2126,0.7152,0.0722), color));
    return vec3(mix(color, gray, amount));
}
void main() {
    vP = vec4(Vert, 1.0);
    gl_Position = ModelViewProjectionMatrix * vec4(Vert, 1.0);
	vec3 l = normalize(LightPos);
	vec3 n = normalize(normalize((NormalMatrix * Norm)));
	vec3 v = normalize(-vec3(ModelViewMatrix*vec4(Vert,1.0)));
	vec3 h = normalize(l+v);
	vec3 a = Clr.rgb;
	vec3 d = a * Diffuse;
	a *= Ambient;
	vec3 backcolor = desaturate(0.75 * a + 0.75 * d *  abs(dot(n,l)), 0.5);
	float backface = 1.0 - step(0.0, n.z); //1=backface
	n = mix(n, -n, backface * float(LightBackfaces)); //reverse normal if backface AND two-sided lighting
	d *= max(pow(max(dot( l, n), 0.0), DiffuseRough), 0.0);
	float s = pow(max(0.0,dot(n,h)), 1.0/(SpecularRough * SpecularRough));
	float w = 0.72*(1.0-Sharpness);
	s = smoothstep(0.72-w,0.72+w,s) * Specular;
	vec3 frontcolor = a + d +  s;
	frontcolor *= min((max(dot(n,normalize(v)), 0.0) - 0.5) * Edge, 0.0) + 1.0;
	backface = 1.0 - step(0.0, n.z); //1=backface
	//backcolor = vec3(1.0,0.0,0.0);
	vClr = vec4(mix(frontcolor,backcolor,   backface), 1.0);
}
//frag
#version 330
uniform vec4 ClipPlane;
in vec4 vClr, vP;
out vec4 color;
void main() {
	if ((ClipPlane[0] < 1.5) && (dot( ClipPlane, vP) > 0.0)) discard;
	color = vec4(vClr.rgb, 1.0);
}
