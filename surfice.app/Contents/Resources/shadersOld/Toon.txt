//pref
Ambient|float|0.0|0.4|1
Diffuse|float|0.0|0.8|1
Specular|float|0.0|0.5|1
Roughness|float|0.001|0.15|0.5
Smooth|float|0.01|1|10
OutlineWidth|float|0.0|0.05|0.2
ShowGradient|bool|false
Hint: set ambient occlusion (AO) to zero. Cel shading, adapted from Philip Rideout work, Copyright 2010, MIT License|note
//vert
varying vec3 vN, vV;
varying vec4 vP, vClr;
void main()
{
    vN = gl_NormalMatrix * gl_Normal;
    vV = -vec3(gl_ModelViewMatrix*gl_Vertex);
    vClr = gl_Color;
    vP = gl_Vertex;
    gl_Position = ftransform();
}
//frag
varying vec3 vN, vV;
varying vec4 vP, vClr;
uniform vec4 ClipPlane;
uniform float Ambient, Diffuse, Specular, Roughness, Smooth, OutlineWidth;
uniform bool ShowGradient;

float stepmix(float edge0, float edge1, float E, float x) {
    float T = clamp(0.5 * (x - edge0) / E, 0.0, 1.0);
    return mix(edge0, edge1, T);
}

vec3 desaturate(vec3 color, float amount) {
    vec3 gray = vec3(dot(vec3(0.2126,0.7152,0.0722), color));
    return vec3(mix(color, gray, amount));
}

void main() {
	if ((ClipPlane[0] < 1.5) && (dot( ClipPlane, vP) > 0.0)) discard;
	vec3 a = gl_FrontMaterial.ambient.rgb;
	a = mix(a.rgb, vClr.rgb, vClr.a);
	vec3 d = a * Diffuse;
	a *= Ambient;
	vec3 n = normalize(vN);
    vec3 l = normalize(gl_LightSource[0].position.xyz);
    float df = dot(n, l);
    vec3 backcolor = desaturate(0.75 * a + 0.75 * abs(df) * d, 0.5);
    vec3 Eye = vec3(0, 0, 1);
    vec3 h = normalize(l + Eye);
    float specular =  pow(max(0.0,dot(n,h)),1.0/(Roughness * Roughness)) * Specular;
	float E = fwidth(specular) * Smooth;
	specular = smoothstep(0.3 - E, 0.3 + E, specular);
    const float A = 0.1;
    const float B = 0.3;
    const float C = 0.6;
    const float D = 1.0;
    E = fwidth(df) * Smooth;
    if (df < A + E) df = stepmix(A, B, E, df);
    else if (df < B + E) df = stepmix(B, C, E, df);
    else df = stepmix(C, D, E, df);
    float outline = step(0.0, OutlineWidth - dot(n,normalize(vV)));
    outline *= step(0.001, OutlineWidth);
    specular = mix(specular, -1.0, outline);
    gl_FragColor.rgb = mix(a + df * d + specular, n + specular, float(ShowGradient) );
	float backface = step(0.0, n.z);
	gl_FragColor = vec4(mix(backcolor.rgb, gl_FragColor.rgb,  backface), 1.0);

}
//http://prideout.net/blog/?p=22
//Unless otherwise stated in the file itself, all source code in
//this folder (and its descendent folders) is covered by the MIT
//License, described as follows:
//
//Copyright (c) 2010 Philip Rideout
//
//Permission is hereby granted, free of charge, to any person
//obtaining a copy of this software and associated documentation
//files (the "Software"), to deal in the Software without
//restriction, including without limitation the rights to use,
//copy, modify, merge, publish, distribute, sublicense, and/or sell
//copies of the Software, and to permit persons to whom the
//Software is furnished to do so, subject to the following
//conditions:
//
//The above copyright notice and this permission notice shall be
//included in all copies or substantial portions of the Software.
//
//THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
//EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
//OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
//NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
//HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
//WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
//FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
//OTHER DEALINGS IN THE SOFTWARE.
