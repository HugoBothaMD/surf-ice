//pref
Ambient|float|0.0|0.5|1
Diffuse|float|0.0|0.7|1
Specular|float|0.0|0.2|1
GridSpacing|float|0.01|2|5
WireWidth|float|0.01|0.15|1.0
xGrid|bool|true
yGrid|bool|true
zGrid|bool|true

Blinn-Phong shading with Lambertian diffuse. Copyright 2015 Chris Rorden, BSD2clause.|note
//vert
varying vec3 vL, vN, vV;
varying vec4 vP, vClr;

void main() {
    vN = normalize(gl_NormalMatrix * gl_Normal);
    vV = -vec3(gl_ModelViewMatrix * gl_Vertex);
    vL = normalize(gl_LightSource[0].position.xyz);
    vP = gl_Vertex;
    gl_Position = gl_ModelViewProjectionMatrix * gl_Vertex;
    vClr = gl_Color;
}
//frag
uniform float GridSpacing, WireWidth, Ambient, Diffuse, Specular;
uniform bool xGrid, yGrid, zGrid;
uniform vec4 ClipPlane;
varying vec3 vL, vN, vV;
varying vec4 vP, vClr;

void main() {
	if ((ClipPlane[0] < 1.5) && (dot( ClipPlane, vP) > 0.0)) discard;
	float W = pow(GridSpacing, 4.0);
	float o = W * WireWidth * 0.5;
	float xDx = mod(vP.x + o, W);
	float yDx = mod(vP.y + o, W);
	float zDx = mod(vP.z + o, W);
	if (!xGrid) xDx = W;
	if (!yGrid) yDx = W;
	if (!zGrid) zDx = W;
	float minDx = min(min(xDx, yDx), zDx);
	if ( ( W * WireWidth) < minDx) discard;

	vec3 l = normalize(vL);
	vec3 n = normalize(vN);
	vec3 h = normalize(l+normalize(vV));
	vec3 a = mix(gl_FrontMaterial.ambient.rgb, vClr.rgb, vClr.a);
	vec3 d = a * Diffuse;
	float diff = dot(n,l);
	a *= Ambient;
	float Shininess = 60.0;
	float s = pow(max(0.0,dot(n,h)), Shininess) * Specular;
	vec3 backcolor = Ambient*vec3(0.1+0.1+0.1) + d * abs(diff);
	float backface = step(0.00, n.z);
	gl_FragColor = vec4(mix(backcolor.rgb, a + d * diff + s,  backface), 1.0);
}
